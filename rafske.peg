lojban-word <- cmene
             / cmavo
             / brivla

brivla <- gismu
       -> brivla-gismu
        / fuhivla
       -> brivla-fuhivla
        / lujvo
       -> brivla-lujvo

;-------------------------------------------------------------------

cmene <- jbocme
       / zifcme

zifcme <- !h
          ( nucleus / glide / h / consonant !pause / digit )*
          consonant
          &pause
       -> zifcme

jbocme <- &zifcme
          ( any-syllable
          / digit )*
          &pause 
       -> jbocme

;-------------------------------------------------------------------

cmavo <- !cmene !CVCy-lujvo cmavo-form &post-word 
      -> cmavo

CVCy-lujvo <- CVC-rafsi
              y
              h?
              initial-rafsi*
              brivla-core
           -> CVCy-lujvo-brivla-core
            / stressed-CVC-rafsi
              y
              short-final-rafsi
           -> CVCy-lujvo-short-final-rafsi

cmavo-form <- !h
              !cluster
              onset
              (nucleus h)*
              ( !stressed nucleus
              / nucleus !cluster )
           -> cmavo-form
            / y+
           -> cmavo-form-y
            / digit

;-------------------------------------------------------------------

lujvo <- !gismu
         !fuhivla
         !cmavo
         initial-rafsi*
         brivla-core
      -> lujvo

brivla-core <- fuhivla
             / gismu
             / CVV-final-rafsi
             / stressed-initial-rafsi short-final-rafsi 

stressed-initial-rafsi <- stressed-extended-rafsi
                        / stressed-y-rafsi
                        / stressed-y-less-rafsi

initial-rafsi <- extended-rafsi
               / y-rafsi
               / !any-extended-rafsi
                 y-less-rafsi

any-extended-rafsi <- fuhivla
                    / extended-rafsi
                    / stressed-extended-rafsi

;-------------------------------------------------------------------

fuhivla <- fuhivla-head
           stressed-syllable
           consonantal-syllable*
           final-syllable
        -> fuhivla

stressed-extended-rafsi <- stressed-brivla-rafsi
                         / stressed-fuhivla-rafsi 

extended-rafsi <- brivla-rafsi
                / fuhivla-rafsi

stressed-brivla-rafsi <- &unstressed-syllable
                         brivla-head
                         stressed-syllable
                         h
                         y
                      -> stressed-brivla-rafsi

brivla-rafsi <- &( syllable
                   consonantal-syllable*
                   syllable )
                brivla-head
                h
                y
                h?
             -> brivla-rafsi

stressed-fuhivla-rafsi <- fuhivla-head
                          stressed-syllable
                          &consonant
                          onset
                          y 
                       -> stressed-fuhivla-rafsi

fuhivla-rafsi <- &unstressed-syllable
                 fuhivla-head
                 &consonant
                 onset
                 y
                 h?
              -> fuhivla-rafsi

fuhivla-head <- !rafsi-string
                brivla-head

brivla-head <- !cmavo
               !slinkuhi
               !h
               &onset
               unstressed-syllable*
            -> brivla-head

slinkuhi <- consonant
            rafsi-string
         -> slinkuhi

rafsi-string <- y-less-rafsi*
                ( gismu
                / CVV-final-rafsi
                / stressed-y-less-rafsi
                  short-final-rafsi
               -> rafsi-string-short-final
                / y-rafsi
                / stressed-y-rafsi
                / stressed-y-less-rafsi?
                  initial-pair
                  y
               -> rafsi-string-initial-pair )
             -> rafsi-string

;-------------------------------------------------------------------

gismu <- ( initial-pair
           stressed-vowel
         / consonant
           stressed-vowel
           consonant )
         &final-syllable
         consonant
         vowel
         &post-word
      -> gismu

CVV-final-rafsi <- consonant
                   stressed-vowel
                   h
                   &final-syllable
                   vowel
                   &post-word
                -> CVV-final-rafsi

short-final-rafsi <- &final-syllable
                     ( consonant
                       diphthong
                     / initial-pair
                       vowel )
                     &post-word
                  -> short-final-rafsi

stressed-y-rafsi <- ( stressed-long-rafsi
                    / stressed-CVC-rafsi )
                    y
                 -> stressed-y-rafsi

stressed-y-less-rafsi <- stressed-CVC-rafsi
                         !y
                       / stressed-CCV-rafsi
                       / stressed-CVV-rafsi

stressed-long-rafsi <- initial-pair
                       stressed-vowel
                       consonant
                    -> stressed-long-rafsi-CCVC
                     / consonant
                       stressed-vowel
                       consonant
                       consonant
                    -> stressed-long-rafsi-CVCC

stressed-CVC-rafsi <- consonant
                      stressed-vowel
                      consonant 
                   -> stressed-CVC-rafsi

stressed-CCV-rafsi <- initial-pair
                      stressed-vowel 
                   -> stressed-CCV-rafsi

stressed-CVV-rafsi <- consonant
                      ( unstressed-vowel
                        h
                        stressed-vowel
                     -> stressed-CVV-rafsi-VhV
                      / stressed-diphthong )
                      r-hyphen? 
                   -> stressed-CVV-rafsi

y-rafsi <- ( long-rafsi
           / CVC-rafsi )
           y
           h?
        -> y-rafsi

y-less-rafsi <- !y-rafsi
                ( CVC-rafsi
                  !y
                / CCV-rafsi
                / CVV-rafsi )
                !any-extended-rafsi

long-rafsi <- initial-pair
              unstressed-vowel
              consonant
           -> long-rafsi-CCVC
            / consonant
              unstressed-vowel
              consonant
              consonant 
           -> long-rafsi-CVCC

CVC-rafsi <- consonant
             unstressed-vowel
             consonant
          -> CVC-rafsi

CCV-rafsi <- initial-pair
             unstressed-vowel
          -> CCV-rafsi

CVV-rafsi <- consonant
             ( unstressed-vowel
               h
               unstressed-vowel
            -> CVV-rafsi-VhV
             / unstressed-diphthong )
               r-hyphen?
          -> CVV-rafsi

r-hyphen <- r &consonant
          / n &r

;-------------------------------------------------------------------

final-syllable <- onset
                  !y
                  !stressed
                  nucleus
                  !cmene
                  &post-word
               -> final-syllable

stressed-syllable <- &stressed
                     syllable
                   / syllable
                     &stress

stressed-diphthong <- &stressed
                      diphthong
                    / diphthong
                      &stress

stressed-vowel <- &stressed
                  vowel
                / vowel
                  &stress

unstressed-syllable <- !stressed
                       syllable
                       !stress
                     / consonantal-syllable

unstressed-diphthong <- !stressed
                        diphthong
                        !stress

unstressed-vowel <- !stressed
                    vowel
                    !stress

stress <- consonant*
          y?
          syllable
          pause

stressed <- onset
            comma*
            [AEIOU]

any-syllable <- onset nucleus coda?
             -> any-syllable
              / consonantal-syllable 

syllable <- onset
            !y
            nucleus
            coda?
         -> syllable

consonantal-syllable <- consonant
                        syllabic
                        &(consonantal-syllable / onset)
                        (consonant &spaces)?
                     -> consonantal-syllable

coda <- !any-syllable
        consonant
        &any-syllable
      / syllabic?
        consonant?
        &pause 
     -> coda

onset <- h
       / consonant?
         glide
      -> onset
       / initial

nucleus <- vowel
         / diphthong
         / y
           !nucleus

;-----------------------------------------------------------------

glide <- ( i / u ) &nucleus !glide

diphthong <- ( a i
             / a u
             / e i
             / o i )
             !nucleus
             !glide
          -> diphthong

vowel <- ( a / e / i / o / u ) !nucleus

a <- comma* [aA] -> a

e <- comma* [eE] -> e

i <- comma* [iI] -> i

o <- comma* [oO] -> o

u <- comma* [uU] -> u

y <- comma* [yY] -> y

;-------------------------------------------------------------------

cluster <- consonant consonant+
        -> cluster

initial-pair <- &initial
                consonant
                consonant
                !consonant
             -> initial-pair

initial <- ( affricate
           / sibilant?
             other?
             liquid?
          -> initial )
           !consonant
           !glide

affricate <- t c -> affricate
           / t s -> affricate
           / d j -> affricate
           / d z -> affricate

liquid <- l
        / r 

other <- p
       / t !l
       / k
       / f
       / x
       / b
       / d !l
       / g
       / v
       / m
       / n !liquid 

sibilant <- c
          / s !x
          / ( j / z )
            !n
            !liquid

consonant <- voiced
           / unvoiced
           / syllabic

syllabic <- l / m / n / r

voiced <- b / d / g / j / v / z

unvoiced <- c / f / k / p / s / t / x

l <- comma* [lL] !h !l -> l

m <- comma* [mM] !h !m !z -> m

n <- comma* [nN] !h !n !affricate -> n

r <- comma* [rR] !h !r -> r

b <- comma* [bB] !h !b !unvoiced -> b

d <- comma* [dD] !h !d !unvoiced -> d

g <- comma* [gG] !h !g !unvoiced -> g

v <- comma* [vV] !h !v !unvoiced -> v

j <- comma* [jJ] !h !j !z !unvoiced -> j

z <- comma* [zZ] !h !z !j !unvoiced -> z

s <- comma* [sS] !h !s !c !voiced -> s

c <- comma* [cC] !h !c !s !x !voiced -> c

x <- comma* [xX] !h !x !c !k !voiced -> x

k <- comma* [kK] !h !k !x !voiced -> k

f <- comma* [fF] !h !f !voiced -> f

p <- comma* [pP] !h !p !voiced -> p

t <- comma* [tT] !h !t !voiced -> t

h <- comma* ['h] &nucleus -> h

;-------------------------------------------------------------------

digit <- comma* [[:digit:]] !h !nucleus
      -> digit

post-word <- pause
           / !nucleus
             lojban-word

pause <- comma*
         space-char+
       / EOF

EOF <- comma* !.

comma <- [,]

non-lojban-word <- !lojban-word non-space+
                -> non-lojban-word

non-space <- !space-char .

space-char <- [[:jboca'u:]]

;-------------------------------------------------------------------

spaces <- ( comma* space-char )+ EOF?
        / EOF

;spaces <- !Y initial-spaces
;
;initial-spaces <- ( comma* space-char
;                  / !ybu Y )+
;                  EOF?
;                / EOF

ybu <- Y `space-char* BU
    -> ybu

;-------------------------------------------------------------------

A    <- &cmavo ( a / e / j i / o / u ) &post-word -> A

BAI  <- &cmavo
        ( d u h o
        / s i h u
        / z a u
        / k i h i
        / d u h i
        / c u h u
        / t u h i
        / t i h u
        / d i h o
        / j i h u
        / r i h a
        / n i h i
        / m u h i
        / k i h u
        / v a h u
        / k o i
        / c a h i
        / t a h i
        / p u h e
        / j a h i
        / k a i
        / b a i
        / f i h e
        / d e h i
        / c i h o
        / m a u
        / m u h u
        / r i h i
        / r a h i
        / k a h a
        / p a h u
        / p a h a
        / l e h a
        / k u h u
        / t a i
        / b a u
        / m a h i
        / c i h e
        / f a u
        / p o h i
        / c a u
        / m a h e
        / c i h u
        / r a h a
        / p u h a
        / l i h e
        / l a h u
        / b a h i
        / k a h i
        / s a u
        / f a h e
        / b e h i
        / t i h i
        / j a h e
        / g a h a
        / v a h o
        / j i h o
        / m e h a
        / d o h e
        / j i h e
        / p i h o
        / g a u
        / z u h e
        / m e h e
        / r a i )
        &post-word
     -> BAI

BAhE <- &cmavo ( b a h e / z a h e ) &post-word -> BAhE

BE   <- &cmavo ( b e ) &post-word -> BE

BEI  <- &cmavo ( b e i ) &post-word -> BEI

BEhO <- &cmavo ( b e h o ) &post-word -> BEhO

BIhE <- &cmavo ( b i h e ) &post-word -> BIhE

BIhI <- &cmavo ( m i h i / b i h o / b i h i ) &post-word -> BIhI

BO   <- &cmavo ( b o ) &post-word -> BO

BOI  <- &cmavo ( b o i ) &post-word -> BOI

BU   <- &cmavo ( b u ) &post-word -> BU

BY   <- ybu
      / &cmavo
        ( j o h o
        / r u h o
        / g e h o
        / j e h o
        / l o h a
        / n a h a
        / s e h e
        / t o h a
        / g a h e
        / y h y
        / b y
        / c y
        / d y
        / f y
        / g y
        / j y
        / k y
        / l y
        / m y
        / n y
        / p y
        / r y
        / s y
        / t y
        / v y
        / x y
        / z y )
        &post-word
     -> BY

CAhA <- &cmavo
        ( c a h a
        / p u h i
        / n u h o
        / k a h e )
        &post-word
     -> CAhA

CAI  <- &cmavo
        ( p e i
        / c a i
        / c u h i
        / s a i
        / r u h e )
        &post-word
     -> CAI

CEI  <- &cmavo ( c e i ) &post-word -> CEI

CEhE <- &cmavo ( c e h e ) &post-word -> CEhE

CO   <- &cmavo ( c o ) &post-word -> CO

COI  <- &cmavo
        ( j u h i
        / c o i
        / f i h i
        / t a h a
        / m u h o
        / f e h o
        / c o h o
        / p e h u
        / k e h o
        / n u h e
        / r e h i
        / b e h e
        / j e h e
        / m i h e
        / k i h e
        / v i h o )
        &post-word
     -> COI

CU   <- &cmavo ( c u ) &post-word -> CU

CUhE <- &cmavo ( c u h e / n a u ) &post-word -> CUhE

DAhO <- &cmavo ( d a h o ) &post-word -> DAhO

DOI  <- &cmavo ( d o i ) &post-word -> DOI

DOhU <- &cmavo ( d o h u ) &post-word -> DOhU

FA   <- &cmavo
        ( f a i
        / f a
        / f e
        / f o
        / f u
        / f i h a
        / f i )
        &post-word
     -> FA

FAhA <- &cmavo
        ( d u h a
        / b e h a
        / n e h u
        / v u h a
        / g a h u
        / t i h a
        / n i h a
        / c a h u
        / z u h a
        / r i h u
        / r u h u
        / r e h o
        / t e h e
        / b u h u
        / n e h a
        / p a h o
        / n e h i
        / t o h o
        / z o h i
        / z e h o
        / z o h a
        / f a h a )
        &post-word
     -> FAhA

FAhO <- &cmavo ( f a h o ) &post-word -> FAhO

FEhE <- &cmavo ( f e h e ) &post-word -> FEhE

FEhU <- &cmavo ( f e h u ) &post-word -> FEhU

FIhO <- &cmavo ( f i h o ) &post-word -> FIhO

FOI  <- &cmavo ( f o i ) &post-word -> FOI

FUhA <- &cmavo ( f u h a ) &post-word -> FUhA

FUhE <- &cmavo ( f u h e ) &post-word -> FUhE

FUhO <- &cmavo ( f u h o ) &post-word -> FUhO

GA   <- &cmavo ( g e h i / g e / g o / g a / g u ) &post-word -> GA

GAhO <- &cmavo ( k e h i / g a h o ) &post-word -> GAhO

GEhU <- &cmavo ( g e h u ) &post-word -> GEhU

GI   <- &cmavo ( g i ) &post-word -> GI

GIhA <- &cmavo
        ( g i h e
        / g i h i
        / g i h o
        / g i h a
        / g i h u )
        &post-word
     -> GIhA

GOI  <- &cmavo
        ( n o h u
        / n e
        / g o i
        / p o h u
        / p e
        / p o h e
        / p o )
        &post-word
     -> GOI

GOhA <- &cmavo
        ( m o
        / n e i
        / g o h u
        / g o h o
        / g o h i
        / n o h a
        / g o h e
        / g o h a
        / d u
        / b u h a
        / b u h e
        / b u h i
        / c o h e )
        &post-word
     -> GOhA

GUhA <- &cmavo
        ( g u h e
        / g u h i
        / g u h o
        / g u h a
        / g u h u )
        &post-word
     -> GUhA

I    <- &cmavo ( i ) &post-word -> I

JA   <- &cmavo ( j e h i / j e / j o / j a / j u ) &post-word -> JA

JAI  <- &cmavo ( j a i ) &post-word -> JAI

JOhI <- &cmavo ( j o h i ) &post-word -> JOhI

JOI  <- &cmavo
        ( f a h u
        / p i h u
        / j o i
        / c e h o
        / c e
        / j o h u
        / k u h a
        / j o h e
        / j u h e )
        &post-word
     -> JOI

KE   <- &cmavo ( k e ) &post-word -> KE

KEhE <- &cmavo ( k e h e ) &post-word -> KEhE

KEI  <- &cmavo ( k e i ) &post-word -> KEI

KI   <- &cmavo ( k i ) &post-word -> KI

KOhA <- &cmavo
        ( d a h u
        / d a h e
        / d i h u
        / d i h e
        / d e h u
        / d e h e
        / d e i
        / d o h i
        / m i h o
        / m a h a
        / m i h a
        / d o h o 
        / k o h a
        / f o h u
        / k o h e
        / k o h i
        / k o h o
        / k o h u
        / f o h a
        / f o h e
        / f o h i
        / f o h o
        / v o h a
        / v o h e
        / v o h i
        / v o h o
        / v o h u
        / r u
        / r i
        / r a
        / t a
        / t u
        / t i
        / z i h o
        / k e h a
        / m a
        / z u h i
        / z o h e
        / c e h u
        / d a
        / d e
        / d i
        / k o
        / m i
        / d o )
        &post-word
     -> KOhA

KU   <- &cmavo ( k u ) &post-word -> KU

KUhE <- &cmavo ( k u h e ) &post-word -> KUhE

KUhO <- &cmavo ( k u h o ) &post-word -> KUhO

LA   <- &cmavo ( l a i / l a h i / l a ) &post-word -> LA

LAU  <- &cmavo ( c e h a / l a u / z a i / t a u ) &post-word -> LAU

LAhE <- &cmavo
        ( t u h a
        / l u h a
        / l u h o
        / l a h e
        / v u h i
        / l u h i
        / l u h e )
        &post-word
     -> LAhE

LE   <- &cmavo
        ( l e i
        / l o i
        / l e h i
        / l o h i
        / l e h e
        / l o h e
        / l o
        / l e )
        &post-word
     -> LE

LEhU <- &cmavo ( l e h u ) &post-word -> LEhU

LI   <- &cmavo ( m e h o / l i ) &post-word -> LI

LIhU <- &cmavo ( l i h u ) &post-word -> LIhU

LOhO <- &cmavo ( l o h o ) &post-word -> LOhO

LOhU <- &cmavo ( l o h u ) &post-word -> LOhU

LU   <- &cmavo ( l u ) &post-word -> LU

LUhU <- &cmavo ( l u h u ) &post-word -> LUhU

MAhO <- &cmavo ( m a h o ) &post-word -> MAhO

MAI  <- &cmavo ( m o h o / m a i ) &post-word -> MAI

ME   <- &cmavo ( m e ) &post-word -> ME

MEhU <- &cmavo ( m e h u ) &post-word -> MEhU

MOhE <- &cmavo ( m o h e ) &post-word -> MOhE

MOhI <- &cmavo ( m o h i ) &post-word -> MOhI

MOI  <- &cmavo
        ( m e i
        / m o i
        / s i h e
        / c u h o
        / v a h e )
        &post-word
     -> MOI

NA   <- &cmavo ( j a h a / n a ) &post-word -> NA

NAI  <- &cmavo ( n a i ) &post-word -> NAI

NAhE <- &cmavo
        ( t o h e
        / j e h a
        / n a h e
        / n o h e )
        &post-word
     -> NAhE

NAhU <- &cmavo ( n a h u ) &post-word -> NAhU

NIhE <- &cmavo ( n i h e ) &post-word -> NIhE

NIhO <- &cmavo ( n i h o / n o h i ) &post-word -> NIhO

NOI  <- &cmavo ( v o i / n o i / p o i ) &post-word -> NOI

NU   <- &cmavo
        ( n i
        / d u h u
        / s i h o
        / n u
        / l i h i
        / k a
        / j e i
        / s u h u
        / z u h o
        / m u h e
        / p u h u
        / z a h i )
        &post-word
     -> NU

NUhA <- &cmavo ( n u h a ) &post-word -> NUhA

NUhI <- &cmavo ( n u h i ) &post-word -> NUhI

NUhU <- &cmavo ( n u h u ) &post-word -> NUhU

PA   <- &cmavo
        ( d a u
        / f e i
        / g a i
        / j a u
        / r e i
        / v a i
        / p i h e
        / p i
        / f i h u
        / z a h u
        / m e h i
        / n i h u
        / k i h o
        / c e h i
        / m a h u
        / r a h e
        / d a h a
        / s o h a
        / j i h i
        / s u h o
        / s u h e
        / r o
        / r a u
        / s o h u
        / s o h i
        / s o h e
        / s o h o
        / m o h a
        / d u h e
        / t e h o
        / k a h o
        / c i h i
        / t u h o
        / x o
        / p a i
        / n o h o
        / n o
        / p a
        / r e
        / c i
        / v o
        / m u
        / x a
        / z e
        / b i
        / s o
        / digit )
        &post-word
     -> PA
  
PEhE <- &cmavo ( p e h e ) &post-word -> PEhE

PEhO <- &cmavo ( p e h o ) &post-word -> PEhO

PU   <- &cmavo ( b a / p u / c a ) &post-word -> PU

RAhO <- &cmavo ( r a h o ) &post-word -> RAhO

ROI  <- &cmavo ( r e h u / r o i ) &post-word -> ROI

SA   <- &cmavo ( s a ) &post-word -> SA

SE   <- &cmavo ( s e / t e / v e / x e ) &post-word -> SE

SEI  <- &cmavo ( s e i / t i h o ) &post-word -> SEI

SEhU <- &cmavo ( s e h u ) &post-word -> SEhU

SI   <- &cmavo ( s i ) &post-word -> SI

SOI  <- &cmavo ( s o i ) &post-word -> SOI

SU   <- &cmavo ( s u ) &post-word -> SU

TAhE <- &cmavo
        ( r u h i
        / t a h e
        / d i h i
        / n a h o )
        &post-word
     -> TAhE

TEhU <- &cmavo ( t e h u ) &post-word -> TEhU

TEI  <- &cmavo ( t e i ) &post-word -> TEI

TO   <- &cmavo ( t o h i / t o ) &post-word -> TO

TOI  <- &cmavo ( t o i ) &post-word -> TOI

TUhE <- &cmavo ( t u h e ) &post-word -> TUhE

TUhU <- &cmavo ( t u h u ) &post-word -> TUhU

UI   <- &cmavo
        ( i h a
        / i e
        / a h e
        / u h i
        / i h o
        / i h e
        / a h a
        / i a
        / o h i
        / o h e
        / e h e
        / o i
        / u o
        / e h i
        / u h o
        / a u
        / u a
        / a h i
        / i h u
        / i i
        / u h a
        / u i
        / a h o
        / a i
        / a h u
        / i u
        / e i
        / o h o
        / e h a
        / u u
        / o h a
        / o h u
        / u h u
        / e h o
        / i o
        / e h u
        / u e
        / i h i
        / u h e
        / b a h a
        / j a h o
        / c a h e
        / s u h a
        / t i h e
        / k a h u
        / s e h o
        / z a h a
        / p e h i
        / r u h a 
        / j u h a
        / t a h o
        / r a h u
        / l i h a
        / b a h u
        / m u h a
        / d o h a
        / t o h u
        / v a h i
        / p a h e
        / z u h u
        / s a h e
        / l a h a
        / k e h u
        / s a h u
        / d a h i
        / j e h u
        / s a h a
        / k a u
        / t a h u
        / n a h i
        / j o h a
        / b i h u
        / l i h o
        / p a u
        / m i h u
        / k u h i
        / j i h a
        / s i h a
        / p o h o
        / p e h a
        / r o h i
        / r o h e
        / r o h o
        / r o h u
        / r o h a
        / r e h e
        / l e h o
        / j u h o
        / f u h i 
        / d a i
        / g a h i
        / z o h o
        / b e h u
        / r i h e
        / s e h i
        / s e h a
        / v u h e
        / k i h a
        / x u
        / g e h e
        / b u h o )
        &post-word
     -> UI

VA   <- &cmavo ( v i / v a / v u ) &post-word -> VA

VAU  <- &cmavo ( v a u ) &post-word -> VAU

VEI  <- &cmavo ( v e i ) &post-word -> VEI

VEhO <- &cmavo ( v e h o ) &post-word -> VEhO

VEhA <- &cmavo
        ( v e h u
        / v e h a
        / v e h i
        / v e h e )
        &post-word
     -> VEhA

VIhA <- &cmavo
        ( v i h i
        / v i h a
        / v i h u
        / v i h e )
        &post-word
     -> VIhA

VUhO <- &cmavo ( v u h o ) &post-word -> VUhO

VUhU <- &cmavo
        ( g e h a
        / f u h u
        / p i h i
        / f e h i
        / v u h u
        / s u h i
        / j u h u
        / g e i
        / p a h i
        / f a h i
        / t e h a
        / c u h a 
        / v a h a
        / n e h o
        / d e h o
        / f e h a
        / s a h o
        / r e h a
        / r i h o
        / s a h i
        / p i h a
        / s i h i )
        &post-word
     -> VUhU

XI   <- &cmavo ( x i ) &post-word -> XI

Y    <- &cmavo ( y+ ) &post-word -> Y

ZAhO <- &cmavo
        ( c o h i
        / p u h o
        / c o h u
        / m o h u
        / c a h o
        / c o h a
        / d e h a
        / b a h o
        / d i h a
        / z a h o )
        &post-word
     -> ZAhO

ZEhA <- &cmavo
        ( z e h u
        / z e h a
        / z e h i
        / z e h e )
        &post-word
     -> ZEhA

ZEI  <- &cmavo ( z e i ) &post-word -> ZEI

ZI   <- &cmavo ( z u / z a / z i ) &post-word -> ZI

ZIhE <- &cmavo ( z i h e ) &post-word -> ZIhE

ZO   <- &cmavo ( z o ) &post-word -> ZO

ZOI  <- &cmavo ( z o i / l a h o ) &post-word -> ZOI

ZOhU <- &cmavo ( z o h u ) &post-word -> ZOhU
